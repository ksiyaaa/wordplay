एक पाठ साहसिक बनाएँ

एक मज़ेदार खेल है/अपना स्वयं का साहसिक कार्य चुनें/खेल, जहां खिलाड़ी कई विकल्पों के बीच से गुजरता है, तथा सुखद अंत का रास्ता ढूंढने का प्रयास करता है।

इनमें से एक बनाने के लिए, हम उपयोग कर सकते हैं @Structure , @Key , @Reaction , और  @List एक साथ मिलकर निर्णयों का एक संग्रह बनाएं, जिनमें से प्रत्येक के लिए विकल्पों का एक सेट हो, जैसे:

\¶इससे एक डेटा संरचना बनती है जो एक विकल्प को संग्रहीत करती है और यह बताती है कि वह किस निर्णय पर पहुंचेगी¶•Option(text•'' decision•#)
¶यह डेटा संरचना विकल्पों की एक सूची संग्रहीत करती है¶•Decision(options: [Option])
¶यह गेम की सामग्री है! माहौल और कठिन विकल्प बनाने के लिए विकल्पों में पाठ बदलें।मुश्किल विकल्पों का जाल बनाने के लिए विकल्प जोड़ें।¶
decisions: [
		Decision([Option('शुरू' 2)])
		Decision([
				Option('बाएं' 3)
				Option('सीधा' 4)
				Option('सही' 4)
			])
		Decision([Option('जीतना' 1)])
		Decision([Option('खोना' 1)])
	]
¶यदि कुंजी एक है तो उसे संख्या में बदलें¶
key: Key()
choice: key → #
¶यह मान ऊपर दी गई सूची में वर्तमान निर्णय को दर्शाता है।जब कुंजी दबायी जाती है, तो हम निम्न में से एक कार्य करते हैं।यदि इस निर्णय के लिए केवल एक ही विकल्प है, तो हम वर्तमान निर्णय को बदल देते हैंविकल्प के लक्ष्य निर्णय पर कोई फर्क नहीं पड़ता कि कौन सी कुंजी दबायी गयी थी।यदि दबायी गयी कुंजी 1 और विकल्पों की संख्या के बीच है, तो हम उस विकल्प के लक्ष्य निर्णय पर जाते हैं।अन्यथा, हम वर्तमान निर्णय को यथावत रखेंगे।¶
current•#: 1 … ∆ key … decisions[current].options.length() = 1 ? decisions[current].options[1].decision ((choice ≥ 1) & (choice ≤ decisions[current].options.length())) ? decisions[current].options[choice].decision current
¶यह विकल्पों को सिर्फ़ वाक्यांशों के ढेर के रूप में दिखाता है। आप निर्देश, एनिमेशन और अन्य रोचकता जोड़ने के लिए इसे बदल सकते हैं!¶Stage([
		Group(Stack() decisions[current].options.translate(ƒ(option) Phrase(option.text)))
	])\

यह पैटर्न जटिल लगता है, लेकिन वास्तव में यह केवल विकल्पों के मौजूदा सेट को दिखाता है और हर बार जब कोई कुंजी बदलती है, तो चुने गए विकल्प के निर्णय पर जाता है। गेम डिज़ाइन करना शीर्ष पर मौजूद सामग्री को बदलने के बारे में है!

offer-choices