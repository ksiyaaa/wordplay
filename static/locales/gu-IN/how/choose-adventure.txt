ટેક્સ્ટ સાહસ બનાવો

એક મનોરંજક પ્રકારની રમત એ છે કે/તમારું પોતાનું સાહસ પસંદ કરો/રમત, જ્યાં ખેલાડી પસંદગીઓની શ્રેણીમાં નેવિગેટ કરે છે, અને સુખદ અંતનો માર્ગ શોધવાનો પ્રયાસ કરે છે.

આમાંથી એક બનાવવા માટે, આપણે ઉપયોગ કરી શકીએ છીએ @Structure , @Key , @Reaction , અને @List સાથે મળીને નિર્ણયોનો સંગ્રહ કરો, દરેક પાસે વિકલ્પોનો સમૂહ હોય, જેમ કે:

\¶આ એક ડેટા સ્ટ્રક્ચર બનાવે છે જે પસંદગી અને તે કયા નિર્ણય પર નેવિગેટ કરશે તે સંગ્રહિત કરે છે.¶•Option(text•'' decision•#)
¶આ ડેટા સ્ટ્રક્ચર પસંદગીઓની યાદી સંગ્રહિત કરે છે.¶•Decision(options: [Option])
¶આ રમતની સામગ્રી છે! વાતાવરણ અને મુશ્કેલ પસંદગીઓ બનાવવા માટે વિકલ્પોમાં ટેક્સ્ટ બદલો.મુશ્કેલ પસંદગીઓનું એક જાળું બનાવવા માટે વિકલ્પો ઉમેરો.¶
decisions: [
		Decision([Option('શરૂઆત' 2)])
		Decision([
				Option('ડાબી બાજુ' 3)
				Option('સીધું' 4)
				Option('ખરું' 4)
			])
		Decision([Option('જીત' 1)])
		Decision([Option('ગુમાવવું' 1)])
	]
¶જો કી એક હોય તો તેને સંખ્યામાં રૂપાંતરિત કરો.¶
key: Key()
choice: key → #
¶આ મૂલ્ય ઉપરોક્ત યાદીમાં વર્તમાન નિર્ણયનું પ્રતિનિધિત્વ કરે છે.જ્યારે કી દબાવવામાં આવે છે, ત્યારે આપણે નીચેનામાંથી એક કરીએ છીએ.જો આ નિર્ણય માટે ફક્ત એક જ વિકલ્પ હોય, તો આપણે વર્તમાન નિર્ણય બદલીએ છીએવિકલ્પના લક્ષ્ય નિર્ણય પર, ભલે ગમે તે કી દબાવવામાં આવી હોય.જો દબાવવામાં આવેલ કી 1 અને વિકલ્પોની સંખ્યા વચ્ચે હોય, તો આપણે તે વિકલ્પના લક્ષ્ય નિર્ણય પર જઈએ છીએ.નહિંતર, અમે ફક્ત વર્તમાન નિર્ણયને એ જ રાખીશું.¶
current•#: 1 … ∆ key … decisions[current].options.length() = 1 ? decisions[current].options[1].decision ((choice ≥ 1) & (choice ≤ decisions[current].options.length())) ? decisions[current].options[choice].decision current
¶આ ફક્ત શબ્દસમૂહોના ઢગલા તરીકે વિકલ્પો બતાવે છે. તમે સૂચનાઓ, એનિમેશન અને અન્ય મસાલેદારતા ઉમેરવા માટે આ બદલી શકો છો!¶Stage([
		Group(Stack() decisions[current].options.translate(ƒ(option) Phrase(option.text)))
	])\

આ પેટર્ન જટિલ લાગે છે, પરંતુ તે ખરેખર ફક્ત વિકલ્પોના વર્તમાન સેટને દર્શાવે છે અને દરેક વખતે કી બદલાય છે, પસંદ કરેલા વિકલ્પના નિર્ણય પર જાય છે. રમત ડિઝાઇન કરવી એ ટોચ પરની સામગ્રી બદલવા વિશે છે!

offer-choices