Направите текстуалну авантуру

Једна забавна врста игре је/изабери своју авантуру/игра, у којој играч пролази кроз низ избора и покушава да пронађе пут до срећног краја.

Да бисмо направили један од ових, можемо користити @Structure , @Key , @Reaction и @List заједно да донесу низ одлука, свака са скупом опција, као што је ова:

\¶Ово ствара структуру података која чува избор и до које одлуке ће водити¶•Option(text•'' decision•#)
¶Ова структура података чува листу избора¶•Decision(options: [Option])
¶Ово је садржај игре! Промените текст у опцијама да бисте створили атмосферу и тешке изборе.Додајте опције да бисте креирали мрежу тешких избора.¶
decisions: [
		Decision([Option('почетак' 2)])
		Decision([
				Option('лево' 3)
				Option('равно' 4)
				Option('право' 4)
			])
		Decision([Option('победити' 1)])
		Decision([Option('изгубити' 1)])
	]
¶Претвори кључ у број, ако је један¶
key: Key()
choice: key → #
¶Ова вредност представља тренутну одлуку на горњој листи.Када се притисне тастер, радимо једно од следећег.Ако постоји само један избор за ову одлуку, онда мењамо тренутну одлукудо циљне одлуке опције без обзира на то који је тастер притиснут.Ако је притиснути тастер између 1 и броја опција, онда прелазимо на циљну одлуку те опције.У супротном, само задржавамо тренутну одлуку исту.¶
current•#: 1 … ∆ key … decisions[current].options.length() = 1 ? decisions[current].options[1].decision ((choice ≥ 1) & (choice ≤ decisions[current].options.length())) ? decisions[current].options[choice].decision current
¶Ово само приказује опције као низ фраза. Можете ово променити да бисте додали упутства, анимације и друге занимљивости!¶Stage([
		Group(Stack() decisions[current].options.translate(ƒ(option) Phrase(option.text)))
	])\

Овај образац изгледа сложено, али све што заправо ради јесте да приказује тренутни скуп опција и сваки пут када се кључ промени, прелази на одлуку о изабраној опцији. Дизајнирање игре се своди на промену садржаја на врху!

offer-choices